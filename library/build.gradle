apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

group='com.nostra13.universalimageloader'
version = project.VERSION
android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 5
        targetSdkVersion 22
        versionCode 40
        versionName project.VERSION
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0-rc3'
    testCompile 'com.squareup.assertj:assertj-android:1.0.0'
}


// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
artifacts {
    archives sourcesJar
}

// Build a jar file in addition to the default aar file
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

apply from: '../gradle/maven_push.gradle'

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('signing.properties').newDataInputStream())
    user = properties.getProperty('BINTRAY_USER')
    key = properties.getProperty('BINTRAY_TOKEN')

    configurations = ['archives'] //When uploading configuration files
    pkg {
        userOrg = 'topface'
        repo = 'libs'
        name = 'com.nostra13.universalimageloader:library'
        desc = 'Android-Universal-Image-Loader forked by Topface'
        websiteUrl = 'https://github.com/Topface/Android-Universal-Image-Loader'
        issueTrackerUrl = 'https://github.com/Topface/Android-Universal-Image-Loader/issues'
        vcsUrl = 'https://github.com/Topface/Android-Universal-Image-Loader.git'
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'imageloader']
        publicDownloadNumbers = true
        version {
            name = project.VERSION //Bintray logical version name
            vcsTag = project.VERSION
        }
    }
}
